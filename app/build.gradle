plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

Properties properties = new Properties()
InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream();
properties.load(inputStream)
//加载Android配置信息
project.ext.storeFileName = properties.getProperty("STORE_FILE_NAME")
project.ext.keyPassword = properties.getProperty("KEY_PASSWORD")
project.ext.storeAlias = properties.getProperty("STORE_ALIAS")
//SimpleRouter路由配置信息
project.ext.simpleRouterKey = properties.getProperty("SIMPLE_ROUTER_KEY")
project.ext.openAes = properties.getProperty("OPEN_AES")

android {
    compileSdkVersion Integer.parseInt(COMPILE_SDK_VERSION)
    buildToolsVersion BUILDTOOLS_VERSION

    defaultConfig {
        applicationId "com.laydown.srouter"
        minSdkVersion Integer.parseInt(MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(TARGET_SDK_VERSION)
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        SIMPLE_ROUTER_KEY: project.ext.simpleRouterKey,
                        OPEN_AES         : project.ext.openAes
                ]
            }
        }
        buildConfigField "String", "SIMPLE_ROUTER_KEY", "\"${project.ext.simpleRouterKey}\""
        buildConfigField "Boolean", "OPEN_AES", "Boolean.parseBoolean(\"${project.ext.openAes}\")"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            storeFile file(project.ext.storeFileName)
            storePassword project.ext.keyPassword
            keyAlias project.ext.storeAlias
            keyPassword project.ext.keyPassword
        }
        release {
            storeFile file(project.ext.storeFileName)
            storePassword project.ext.keyPassword
            keyAlias project.ext.storeAlias
            keyPassword project.ext.keyPassword
        }
    }

    buildTypes {
        deubg {
            zipAlignEnabled true
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        release {
            zipAlignEnabled true
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }


    android.applicationVariants.all {
        variant ->
            variant.outputs.all {
                outputFileName = "SimpleRouter_V${android.defaultConfig.versionName}_${variant.name}.apk"
            }
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation project(path: ':biz-shop')
    implementation project(path: ':biz-tax')
    implementation project(path: ':lib-provider')
    if (Boolean.parseBoolean(ROUTER_DEBUG_MODE)) {
        implementation project(path: ':srouter-annotation')
        implementation project(path: ':srouter-api')
        kapt project(path: ':srouter-compiler')
    } else {
        implementation "com.github.jeff-liu14:srouter-annotation:$SROUTER_ANNOTATION"
        implementation "com.github.jeff-liu14:srouter-api:$SROUTER_API"
        kapt "com.github.jeff-liu14:srouter-compiler:$SROUTER_COMPILER"
    }
}